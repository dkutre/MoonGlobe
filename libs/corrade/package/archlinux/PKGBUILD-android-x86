# Author: mosra <mosra@centrum.cz>
pkgname=android-x86-corrade
pkgver=dev
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (Android x86)"
arch=('any')
url="http://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'ninja' 'android-ndk')
options=(!strip !buildflags staticlibs)

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-android-x86" ] ; then
        mkdir "$_rootdir/build-android-x86"
        cd "$_rootdir/build-android-x86"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Android-x86.cmake" \
            -G Ninja
    fi

    cd "$_rootdir/build-android-x86"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/opt/android-ndk/platforms/android-19/arch-x86/usr \
        -DBUILD_TESTS=ON
    ninja
}

package() {
    cd "$_rootdir/build-android-x86"
    DESTDIR="$pkgdir/" ninja install/strip
}
