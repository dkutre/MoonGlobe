# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-corrade
pkgver=dev.noopt
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (Emscripten, w/o optimizations)"
arch=('any')
url="http://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'emscripten' 'ninja')
options=(!strip !buildflags)

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten-noopt" ] ; then
        mkdir "$_rootdir/build-emscripten-noopt"
        cd "$_rootdir/build-emscripten-noopt"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten.cmake" \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten-noopt"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DCMAKE_CXX_FLAGS_RELEASE="-DNDEBUG -O1" \
        -DCMAKE_EXE_LINKER_FLAGS_RELEASE="-O1" \
        -DBUILD_TESTS=ON
    ninja
}

check() {
    cd "$_rootdir/build-emscripten-noopt"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-emscripten-noopt"
    DESTDIR="$pkgdir/" ninja install
}
