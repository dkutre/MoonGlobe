# Author: mosra <mosra@centrum.cz>
pkgname=android-arm-magnum-plugins
pkgver=dev
pkgrel=1
pkgdesc="Plugins for Magnum OpenGL graphics engine (Android ARM)"
arch=('any')
url="http://magnum.graphics"
license=('MIT')
depends=('android-arm-magnum')
makedepends=('cmake' 'ninja' 'android-ndk' 'corrade')
options=('!strip' '!buildflags')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-android-arm" ] ; then
        mkdir "$_rootdir/build-android-arm"
        cd "$_rootdir/build-android-arm"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Android-ARM.cmake" \
            -G Ninja
    fi

    cd "$_rootdir/build-android-arm"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/opt/android-ndk/platforms/android-19/arch-arm/usr \
        -DWITH_ANYAUDIOIMPORTER=OFF \
        -DWITH_ANYIMAGECONVERTER=ON \
        -DWITH_ANYIMAGEIMPORTER=ON \
        -DWITH_ANYSCENEIMPORTER=ON \
        -DWITH_ASSIMPIMPORTER=OFF \
        -DWITH_DDSIMPORTER=ON \
        -DWITH_DEVILIMAGEIMPORTER=OFF \
        -DWITH_DRFLACAUDIOIMPORTER=OFF \
        -DWITH_DRWAVAUDIOIMPORTER=OFF \
        -DWITH_MINIEXRIMAGECONVERTER=ON \
        -DWITH_OPENGEXIMPORTER=ON \
        -DWITH_STANFORDIMPORTER=ON \
        -DWITH_STBIMAGECONVERTER=ON \
        -DWITH_STBIMAGEIMPORTER=ON \
        -DWITH_STBTRUETYPEFONT=ON \
        -DWITH_STBVORBISAUDIOIMPORTER=OFF \
        -DBUILD_TESTS=ON
    ninja
}

package() {
    cd "$_rootdir/build-android-arm"
    DESTDIR="$pkgdir/" ninja install/strip
}
