# Author: mosra <mosra@centrum.cz>
pkgname=android-arm-magnum
pkgver=dev
pkgrel=1
pkgdesc="C++11/C++14 and OpenGL 2D/3D graphics engine (Android ARM)"
arch=('any')
url="http://magnum.graphics"
license=('MIT')
depends=('android-arm-corrade')
makedepends=('cmake' 'ninja' 'android-ndk' 'corrade')
options=('!strip' '!buildflags')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-android-arm" ] ; then
        mkdir "$_rootdir/build-android-arm"
        cd "$_rootdir/build-android-arm"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Android-ARM.cmake" \
            -DTARGET_GLES=ON \
            -DTARGET_GLES2=ON \
            -G Ninja
    fi

    cd "$_rootdir/build-android-arm"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/opt/android-ndk/platforms/android-19/arch-arm/usr \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_OBJIMPORTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_ANDROIDAPPLICATION=ON \
        -DWITH_EGLCONTEXT=ON \
        -DBUILD_TESTS=ON
    ninja
}

package() {
    cd "$_rootdir/build-android-arm"
    DESTDIR="$pkgdir/" ninja install/strip
}
